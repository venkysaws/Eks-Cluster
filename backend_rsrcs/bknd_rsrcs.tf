provider "aws" {
  region = "ap-south-1"
}


resource "aws_s3_bucket" "eks_buck" {
  bucket = "eksprojbackendvenkaiah"

  lifecycle {
    prevent_destroy = true
  }
}

resource "aws_s3_bucket_versioning" "eks_buck" {
  bucket = aws_s3_bucket.eks_buck.id

  versioning_configuration {
    status = "Enabled"
  }
}

resource "aws_s3_bucket_server_side_encryption_configuration" "eks_buck" {
  bucket = aws_s3_bucket.eks_buck.id

  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "AES256"
    }

    bucket_key_enabled = true
  }
}

resource "aws_s3_bucket_public_access_block" "eks_buck" {
  bucket = aws_s3_bucket.eks_buck.id

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}
data "aws_caller_identity" "current" {}

resource "aws_s3_bucket_policy" "eks_buck" {
  bucket = aws_s3_bucket.eks_buck.id

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid       = "bucketpolicytoallowhttps"
        Effect    = "Deny"
        Principal = "*"
        Action    = "s3:*"
        Resource = [
          aws_s3_bucket.eks_buck.arn,
          "${aws_s3_bucket.eks_buck.arn}/*"
        ]
        Condition = {
          Bool = {
            "aws:SecureTransport" = "false"
          }
        }
      },
      {
        Sid    = "bucketpolicytoallowid"
        Effect = "Allow"
        Principal = {
          AWS = "arn:aws:iam::${data.aws_caller_identity.current.account_id}:root"
        }
        Action = [
          "s3:GetObject",
          "s3:PutObject",
          "s3:DeleteObject",
          "s3:ListBucket"
        ]
        Resource = [
          aws_s3_bucket.eks_buck.arn,
          "${aws_s3_bucket.eks_buck.arn}/*"
        ]
      }
    ]
  })
}

resource "aws_dynamodb_table" "eksdynstatlock" {
  name           = "statelock"
  billing_mode   = "PROVISIONED"
  read_capacity  = 20
  write_capacity = 20
  hash_key       = "LockID"

  attribute {
    name = "LockID"
    type = "S"
  }


  tags = {
    Name        = "dynamodb-table-1"
    Environment = "production"
  }
}
